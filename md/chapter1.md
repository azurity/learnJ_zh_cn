# 第一章：基础

## 1.1 交互使用

使用者通过键盘键入一行内容。输入的这一行内容可以是表达式，例如`2+2` 。这一行结束后（按下回车键），表达式的值将被计算出来并显示在下一行。

```
    2+2
4
```

之后将提示用户输入下一行。作为提示，光标将被定位在距左侧边界几个空格。因此，在本书中有几个空格作为缩进的一行表示用户的输入，而接下来没有没有缩进的一行表示相应的输出。

## 1.2 计算

乘法的符号是 `*` (星号)。

```
    2*3
6
```

如果在尝试一次，这次输入 `2 空格 * 空格 3`

```
    2 * 3
6
```

结果和之前一样，显然这里的空格是可选的。空格可以让表达式更具有可读性。

除法的符号是`%`(百分号)。

```
    3 % 4
0.75
```

对于减法，我们使用熟悉的符号`-`

```
    3 - 2
1
```

下一个例子展示负数的表示方法。负数以 _ (下划线) 符号开头，在符号与数字之间没有空格。这个符号不是不是一个算数函数，而是数字表示方法的一部分。同样，小数点也是这种表示方法的一部分。

```
    2 - 3
_1
```

负数运算的符号是`-`，与减法运算相同：

```
    -3
_3
```

幂运算的符号是`^`(增补号)。2的立方是8：

```
    2 ^ 3
8
```

求一个数平方的算术函数，其符号是`*:`(星号 冒号)。

```
    *: 4
16
```

## 1.3 一些术语：函数(function)，参数(argument)，应用(application)，值(value)

考虑一个表达式，比如`2 * 3`。我们称这是乘法函数`*`被应用到它的参数上。左参数是`2`，右参数是`3`。同时，`2`和`3`被成为参数的值。

## 1.4 一列值

有时我们希望多次进行相同的运算但使用不同的数。一列数可以像`1 2 3 4`这样给出，在每个两个数字间写一个空格。分别计算一列数中每个数的平方，我么可以这样写：

```
    *: 1 2 3 4
1 4 9 16
```

在这里我们看到“平方”函数 (`*:`)分别应用到了一列数的每一项。如果将两个列参数给一个像 + 这样的函数，函数将分别应用到每一对相对应的项：

```
1 2 3 + 10 20 30
11 22 33
```

如果一个参数是列参数另一个是一个单独的项，单独的项将被按需复制：

```
    1 + 10 20 30
11 21 31

    1 2 3 + 10
11 12 13
```

当我们面对一个新函数，有时了解一列参数以及得到的一列结果的模式会对了解这个函数很有帮助。

例如，当`7`除以`2`时，商是`3`，余数是`1`。J语言中被用于求余数的内置函数是`|`(竖线)，叫做“求余”函数。参数以及结果的模式如下：

```
    2 | 0 1 2 3 4 5 6 7
0 1 0 1 0 1 0 1

    3 | 0 1 2 3 4 5 6 7
0 1 2 0 1 2 0 1
```

求余函数就像是熟悉的“求模”函数，除了要写成`(2 | 7)`而不是`(7 mod 2)`

## 1.5 括号

一个表达式可以包含拥有通常含义的括号；在括号内的表达式会进行单独的计算。

```
    (2+1)*(2+2)
12
```

然而并不总是需要括号。考虑如下J语言表达式：`3*2+1`。这意味着`(3*2)+1`，结果为`7`，还是意味着`3*(2+1)`结果为`9`? 

```
    3 * 2 + 1
9
```

在学校教授的数学中提到了一条写表达式的准则：先乘除后加减。这个规则减少了我们所需要写的括号的数目。 

但是在J语言中并不存在诸如先乘除后加减的规则。我们总要在需要写括号的地方写下括号。然而，正如`3*2+1`所展示的这样，在J语言中有一个节省括号的规则。在没有括号时的规则是这样的：一个算术函数的右参数是它右侧的整个表达式。因此，对于`3*2+1`，`*`的右参数是`2+1`。接下来是另一个例子：

```
    1 + 3 % 4 
1.75
```

我们可以看到`%`在`+`之前应用。也就是说，最右侧的函数最先被应用。

这个“最右最先”的规则与通常情况下的“先乘后加”规则不同，但却起着类似的作用。这仅仅是为了方便而已。你完全可以忽视这个规则，并使用括号代替。它的优势在于在J语言中有许多（比如100个）数值计算函数，很难去记住在这之中谁先被应用。

在本书中，我会适时的给出有括号的表达式，因而并不会用上“最右最先”原则。这样做的目的是通过切断表达式来突显出它的的结构，使之更便于阅读。

## 1.6 变量与赋值

在正常语言中的表达：

“令x等于100”在J语言中表达方式如下：

```
    x =: 100
```

这个表达式被叫做赋值表达式，它使`100`这个值被赋给命名`x`。我们说一个叫作 x 的变量被创建并拥有`100`这个值。当输入的一行只包含赋值时，将不会返回任何东西。

有指定值的命名可以在任何地方使用这个值。

```
    x - 1
99
```

赋值所使用的值本身可以通过表达式计算获得：

```
    y =: x - 1
```

于是变量`y`被用来代表`x-1`的计算结果。若要查看一个变量的值，只需要输入它的名称即可。这有一个像其它一样特别简单的表达式：

```
    y
99
```

可以重复指定同一个变量。这样做的话，新的值将取代现有的值： 

```
    z=: 6
    z =: 8
    z
8
```

一个变量的值可以用于计算这个变量的新值：

```
    z =: z + 1
    z
9
```

如上所示，当以赋值作为一行输入时，其值并不会显示。然而，赋值是一种表达式：它确实有着一个可以参与更大的表达式的值。

```
    1 + (u =: 99)
100
    u
99
```

接下来是一些例子关于如何选择变量名： 

```
x =: 0
X =: 1
K9 =: 2
finaltotal =: 3
FinalTotal =: 4
average_annual_rainfall =: 5
```

每一个名字都必须以字母开头。这个名字仅可以包含字母（包括大写和小写），数字(`0-9`)以及下划线(`_`) 。注意：大写和小写字母并不相同， x 和 X 是两个不同的变量名： 

```
    x
0

    X
1
```

## 1.7 术语：单目(Monads)和双目(Dyads) 

一个仅在右侧有一个参数的函数称之为单目函数(monadic function)，或者简称为(monad) 。一个例子就是"平方"`(*:)` 。一个在左右两侧各有一个参数的函数称之为双目函数(dyadic function)或简称为(dyad)。一个例子就是`+`。 

减法和取负是一个使用相同符号(-)表示不同功能的例子。换句话说，我们可以说-有单目的情况(取负)和双目的情况(减法)。几乎所有J语言的内置函数都有单目情况和双目情况。例如，回想一下除法的符号是`%`，或者说`%`的双目情况。`%`的单目情况是取倒数。

```
    % 4
0.25
```

## 1.8 更多内置函数

本节的目的是是通过稍进一步讲解许多内置函数中的几个来传达一点J语言编程的味道。

考虑下面这个自然语言的表达式：把2，3和4求和，或者更简单的表示形式：

add together(求和) 2 3 4

我们所期望的结果是9。在J语言里，这个表达式呈现如下：

```
    + / 2 3 4
9
```

将英语和J语言对比，"add" 用`+`来表示，"together"用`/`来表示。同样，我们下面这个表达式：

multiply together(连乘) 2 3 4

应该等于24。在J语言里表示如下：

```
    * / 2 3 4
24
```

我们可以看到`+/2 3 4`表示`2+3+4`，`*/2 3 4`表示 2*3*4 。`/`符号叫作"插入"，缘于在实际上它会把它左面的函数插入到它右侧列表的每个空隙中。一般的情况如下：如果`F`是一个双目函数，`L`是一列如同`a, b, c, .... y, z`这样的数，那么：

```
F / L 意味着 a F b F .... F y F z 
```

把视线转向更多的函数，考虑一下这三个命题：

2 大于 1 (明显是真的(true))

2 等于 1 (假的(false))

2 小于 1 (假的(false))

在J语言中，“真(true)”使用数字`1`来表示，“假(false)”使用数字`0`。这三个命题在J语言中表达如下：

```
    2 > 1
1

    2 = 1
0

    2 < 1
0
```

如果`x`是一列数字，例如：

```
    x =: 5 4 1 9 
```

我们可以问：`x`中哪个数字大于`2`? 

```
    x > 2
1 1 0 1
```

显然，第一个，第二个和最后一个反馈了1作为`x > 2`的结果。那么，是否`x`中所有数字都大于 2?

```
    * / x > 2
0
```

结果是否定的。因为可以看到`x>2`的结果为`1 1 0 1`。任何零("false")的出现意味着乘法(在这里是`1*1*0*1`)不能得到1的结果。

`x`中有多少项大于 2? 我们将`x>2`中的1相加：

```
    + / x > 2
3
```

在 x中共有多少项?我们可以将`x=x`中的1相加。

```
    x
5 4 1 9

    x = x
1 1 1 1

    +/ x = x
4
```

但是有另一个内置函数`#`(叫作“计数”) 可以直接给出列表的长度：

```
    # x
4
```

## 1.9 并排展示

当我们向电脑输入J语言表达式，表达式与结果沿屏幕向下显示。让我再给你看看最后几行。

```
    x
5 4 1 9
    x = x
1 1 1 1
    +/ x = x
4
    # x
4
```

现在，在这本书中，有时我想要像这样，不是向下延伸，而是并排的展示几行。

`x`      |`x = x`  |`+/ x = x` |`# x` 
---------|---------|-----------|----
`5 4 1 9`|`1 1 1 1`|`4`        |`4`   

这表示：在当前情况下，如果你输入表达式`x`你就能看到`5 4 1 9`这个结果。如果你输入`x = x`就会看到`1 1 1 1`，其它也类似。并排展示并不是J语言环境(J system)的一部分功能，而是这本书的数字或插图。这将在第一行显示表达式，在第二行显示对应的值。

当你输入一个赋值表达式(`x=:something`)，J语言环境不会显示值。然而，赋值是表达式并拥有相应的的值。然而看到并被提醒赋值得到的值对我们是有帮助的，所以我也会在并列展示时一并显示赋值表达式的值。就像这样：

`x =: 1 + 2 3 4`|`x = x`|`+/ x = x`|`# x`
----------------|-------|----------|------
`3 4 5`         |`1 1 1`|`3`       |`3` 

现在回过来再看内置函数，假设我们有一列数。然后我们可以通过说“要，要，不要，要，不要”的方式来从中选择几项。我们用来选择的队列使用`1 1 0 1 0`来表示。这样的0和1组成的列表被叫做位串(bit-string)(有时也叫做bit-list 或 bit-vector)。有一个函数，双目的`#`，可以以位串作为左参数来从右参数中选择项。
 
`y =: 6 7 8 9 10`      |`1 1 0 1 0 # y`   
-----------------------|---------------
`6 7 8 9 10`           |`6 7 9`

我们也可以从`y`中仅选择符合某些条件的项，例如选择大于`7`的。

`y`         |`y > 7`    |`(y > 7) # 7`   
------------|-----------|---
`6 7 8 9 10`|`0 0 1 1 1`|`8 9 10`
 
## 1.10 注释

在J语言的一行里，符号`NB.`(大写的N，大写的B，点)作为注释的开头。在从 NB. 到一行结束之间的任何内容都不会被计算。例子如下：

```
    NB. this is a whole line of annotation 
    6 + 6 NB. ought to produce 12
12
```

## 1.11 内置函数的命名规则

J语言中的每个内置函数都有一个正式的和一个非正式的名字。例如，正式名字是`+`的内置函数，非正式的名字是“加”。另外，可能会有单目与双目的情况，所以正式名字是`-`相当于非正式名字“取负”和“减”。

实际上，非正式名字是仅使用一个词的简短描述。非正式名字并不被J语言软件认可，所以在J语言表达式里要使用正式名称。在本书中，非正式名称使用引号，例如“减”。

几乎所有的J语言内置函数都有一个一或两个字符的正式名称。例如`*`和`*:`函数。第二个字符总是`:`(冒号) 或`.`(点)。

一个两字符名称意味着它和它所基于的一字符函数有一定的关系。因此“平方”(`*:`) is与“乘”(`*`)有一定联系。

因此每一组J语言内置函数拥有六个相互关联的功能。这其中包括单目与双目的情况，每种情况又包含基础，冒号和点的变体。这可以使用`>`一组函数来说明。

双目`>`我们已经遇到了，表示“大于”。

单目`>`我们之后再讲。

单目`>.`将参数变换为整数。注意，舍入总是向上，而非四舍五入。因此被称之为“向上取整”。

```
    >. _1.7 1 1.7
_1 1 2
```

双目`>.`选择两个参数中较大的一个。

```
    3 >. 1 3 5 
3 3 5
```

我们可以通过使用`` 在一列数中间插入“取大值”来获取最大值。例如，通过计算`(>. / 1 6 5)`在`1 6 5`中寻找最大值。接下来的几行将说明这是如何得到`6`的。

```
    >. / 1 6 5
6

    1 >. 6 >. 5 NB. 由 / 的意义得到
6

    1 >. (6 >. 5) NB. 由于最右最先的原则
6

    1 >. (6) NB. 由 >. 的意义得到
6

    1 >. 6 NB. 由 () 的意义得到
6

    6 NB. 由 >. 的意义得到
6
```

单目`>:`非正式的名字是“增加”。它会将每个参数加1：

```
    >: _2 3 5 6.3
_1 4 6 7.3
```

双目`>:`表示“大于等于”。

```
    3 >: 1 3 5 
1 1 0
```