# 第二章：列(list)与表(table)

计算需要数据。到目前为止，我们看到的数据仅仅是单个数字或者是一列数字而已。我们可以使用其他的方式表示数据，比如说下面这个表。想这些列和表之类的，统称为“数组”。

>译者注：有时列（list）会根据不同语意翻译为“一列数”或“列表”

## 2.1 表

这是一个两行三列的表，我们可以使用`$`函数来构建：

```
    table =: 2 3 $ 5 6 7 8 9 10

    table
5 6 7
8 9 10
```

这里的方案是使用`(x $ y)`这样一个表达式来构建一个表。表格的尺寸由列表`x`来给出，列表中第一个数是行数，然后是列数。表中每一个元素则是由列表`y`来提供。

表格项从`y`中依次获取，首先填充第一行，之后是下一行，然后依次类推。这一列数`y`至少要包含一个项。如果在`y`中的项数过少而不能填满整个表格的话，`y`就会被从头开始循环使用，来填满整个表格。

`2 4 $ 5 6 7 8 9`     |`2 2 $ 1`
----------------------|-------
`5 6 7 8`<br>`9 5 6 7`|`1 1`<br>`1 1` 

`$`函数只是提供了一种构建表格的方法，更多的方法详见[第五章](chapter5.md)。

函数可以被应用到整个表格上，这和之前在列上的应用方式是一样的。

`table`            |`10 * table`             |`table + table`
-------------------|-------------------------|---------------
`5 6 7`<br>`8 9 10`|`50 60 70`<br>`80 90 100`|`10 12 14`<br>`16 18 20`

也可以一个参数是表，另一个参数是列：

`table`            |`0 1 * table`
-------------------|-------------
`5 6 7`<br>`8 9 10`|`0 0 0`<br>`8 9 10` 

在最后这个例子中，显然可见列表`0 1`中的项自动与表中的行相匹配，`0`和第一行关联`1`和第二行关联。两个参数之间其他的匹配方式也是可能的，详见[]第七章](chapter7.md)。

## 2.2 数组

一个表有两个维度（行和列），所以在某种意义上列（list）可以被认为只有一个维度。

我们可以使用超过两个维度的表格样（table-like）数据。`$`函数的左参数可以是任意长度的一列数。“数组”这个名词被用来通指有着一些维度的数据。接下来是一些一维二维和三维的数组。

`3 $ 1`|`2 3 $ 5 6 7`     |`2 2 3 $ 5 6 7 8`
-------|------------------|-------------------------------
`1 1 1`|`5 6 7`<br>`5 6 7`|`5 6 7`<br>`8 5 6`<br><br>`7 8 5`<br>`6 7 8`

最后一个例子中的三维数组被称为有2个平面，2行，3列。而对于两个平面，一个显示在另一个下面。

回忆一下，单目函数`#`可以给出列表长度。

`# 6 7`|`# 6 7 8`
-------|--------
`2`    |`3`
 
单目函数`$`可以给出其参数的维度列表(list-of-dimensions)

`L =: 5 6 7`|`$ L`|`T =: 2 3 $ 1`    |`$ T`
------------|-----|------------------|-----
`5 6 7`     |`3`  |`1 1 1`<br>`1 1 1`|`2 3`

因此，如果 `x`是一个数组，表达式`(# $ x)`将得到`x`维度列表的长度，也就是`x `维数（dimension-count）。对于列来说是`1`对于表来说是`2`。

`L`    |`$ L`|`# $ L`|`T`               |`$T` |`# $ T`
-------|-----|-------|------------------|-----|---
`5 6 7`|`3`  |`1`    |`1 1 1`<br>`1 1 1`|`2 3`|`2` 

如果我们让 x 只是一个数字，那么表达式`(# $ x)`将得到0。

```
    # $ 17
0
```

对于这个的意义，我们给出如下解释，一个表有两个维度，一个列有一个维度，而一个数字并没有维度，因此维数是零。一个维数是零的数据对象被叫做一个标量。我们认为“数组”是一个有着一些维的数据对象，所以标量也是数组只不过是维数为零的情况。

我们已经知道`(# $ 17)`等于`0`。我么也就可以做出如下推断：因为标量是没有维度，所以它的维度列表(比如用`$ 17`取得)一定是一个长度为零的（或者说是空的）列表。一个长为2的列表，可以使用`2 $ 99`来得到，所以一个长为0的列表可以使用`0 $ 99`(甚至是`0 $`任意数字)来得到。

`2 $ 99`|`0 $ 99`|`$ 17`
--------|--------|-----
`99 99` |        |

空列的值就被显示为什么都没有：
 
注意：对于一个标量(比如说`17`)，它和一个长为1的列表(比如`1 $ 17`)或一个1行1列的表(比如`1 1 $ 17`)并不一样。标量没有维度，列有一个维度，表有两个。虽然三者在屏幕上显示是一样的。

```
    S =: 17
    L =: 1 $ 17
    T =: 1 1 $ 17
```

`S` |`L` |`T` |`# $ S`|`# $ L`|`# $ T`
----|----|----|-------|-------|--------
`17`|`17`|`17`|`0`    |`1`    |`2` 

一个表就算只有一列，也依旧有两个维度。这里的`t`有3行1列。

`t =: 3 1 $ 5 6 7`|`$ t`|`# $ t`
------------------|-----|------
`5 6 7`           |`3 1`|`2` 

## 2.3 术语：阶(Rank)和外形 (Shape)

我们称之为“维数”的特征在J语言中被简称为“阶”。因此，一个单个的数就是一个0阶（rank-0）数组，列就是1阶数组，依次类推。而维度列表被叫做“外形”。
数学中的术语“向量”和“矩阵”对应于之前所说的“列”和“表”(相对于数字)。对于大于等于三维的数组（或者所至少3阶的数组），称之为“报告(report)”。

关于数组的相关属于和函数的描述如下表所示：

```
+--------+--------+-----------+------+
|        | Example| Shape     | Rank |
+--------+--------+-----------+------+
|        | x      | $ x       | # $ x|
+--------+--------+-----------+------+
| Scalar | 6      | empty list| 0    |
+--------+--------+-----------+------+
| List   | 4 5 6  | 3         | 1    |
+--------+--------+-----------+------+
| Table  |0 1 2   | 2 3       | 2    |
|        |3 4 5   |           |      |
+--------+--------+-----------+------+
| Report |0 1 2   | 2 2 3     | 3    |
|        |3 4 5   |           |      |
|        |        |           |      |
|        |6 7 8   |           |      |
|        |9 10 11 |           |      |
+--------+--------+-----------+------+
```

上面的表实际上是由一个J语言程序生成的，是一个真正的“表”——就是我们刚刚讨论的那种。它的外形是`6 4`. 显然它不仅仅是一个数字的表，其中还包括了词语，列表和其他的东西。我们可以看到数组还可以包含数字之外的的东西。

## 2.4 字符数组

字母标点数字之类的就是字符。我们可以使用字符数组，就好比使用数字数组一样。字符列表用在单引号之间的一些字符表示。其并不包含作为边界的单引号。实例如下：

```
    title =: 'My Ten Years in a Quandary'

    title
My Ten Years in a Quandary
```

字符列表被叫做字符串(character-string)，或者仅仅叫作串(string)。在字符串中的单引号使用两个单引号来表示。

```
    'What''s new?'
What's new?
```

一个空的字符串（长度为零）表示为连续的两个单引号。它不会显示任何字符。

`''`|`# ''`
----|------
    |`0`
 
## 2.5 一些数组函数

现在，我们来看一些数组处理函数是很有意义的。J语言有丰富的数组处理函数，我们先来看一小部分。

### 2.5.1 链接(Joining)

内置函数 , (逗号)被叫做“附加”。它会把参数链接在一起生成列表。

`a =: 'rear'`|`b =: 'ranged'`|`a,b`
-------------|---------------|-----
`rear`       |`ranged`       |`rearranged`
 
“附加”函数可以链接列或者单个的项。

`x =: 1 2 3`|`0 , x`  |`x , 0`  |`0 , 0`|`x , x`
------------|---------|---------|-------|-----
`1 2 3`     |`0 1 2 3`|`1 2 3 0`|`0 0`  |`1 2 3 1 2 3`

“附加”函数可以将两个表首尾相连(end-to-end)生成一个更长的表。

`T1=: 2 3 $ 'catdog'`|`T2=: 2 3 $ 'ratpig'`|`T1,T2`
---------------------|---------------------|-------
`cat<br>dog`         |`rat`<br>`pig`       |`cat`<br>`dog`<br>`rat`<br>`pig`

更多关于“附加”函数的内容，详见[第五章](chapter5.md)。

### 2.5.2 项

列的项就是它的每个数字，而表的项就是它的每行。三维数组的项就是每个平面。总的来讲，项就是一个数组在第一维上顺序出现的事物。一个数组就是它的项的列表。

回忆一下，内置动词`#`(“计数”)会给出一个列表的长度。

`x`    |`# x`
-------|-----
`1 2 3`|`3`
 
一般来书，`#`会统计一个数组的项数。所以，这将得到第一维的大小。

`T1`          |`$ T1`|`# T1`
--------------|------|------
`cat`<br>`dog`|`2 3` |`2`
 
显然`# T1`的得数就是维度列表`$ T1`的第一项。但对于一个没有维度的标量，它将被视为一个项。

```
    # 6
1
```

再重新考虑一下之前“附加”的例子。

`T1`          |`T2`          |`T1 , T2`
--------------|--------------|-------------
`cat`<br>`dog`|`rat`<br>`pig`|`cat`<br>`dog`<br>`rat`<br>`pig` 

现在我们可以认为，通常情况下，`(x , y)`表示一个`x`当中的项后面接`y`当中项的列表。

另一个关于“项”的有用例子，是关于可以将+插进列表中项之间的动词`+/`。

 
接下来我们可以看到，`+/`把`+`插到数组的两项之间。在下面这个例子中，是两行之间。

`T =: 3 2 $ 1 2 3 4 5 6`|`+/ T`|`1 2 + 3 4 + 5 6`
------------------------|------|-----------------
`1 2`<br>`3 4`<br>`5 6` |`9 12`|`9 12`  

### 2.5.3 选择

接下来，我们考虑从列中选择项。在列表中，项的位置用`0, 1, 2`等等的数标识。第一个项的位置是`0`。

我们使用函数`{`(左花括号，被叫做“源于”)来通过位置选择项。

`Y =: 'abcd'`|`0 { Y`|`1 { Y`|`3 { Y`
-------------|-------|-------|-------
`abcd`       |`a`    |`b`    |`d`

标识位置的数字(position-number)被叫做序号(index)。`{`函数的左参数可以是一个或一列序号：

`Y`   |`0 { Y`|`0 1 { Y`|`3 0 1 { Y`
------|-------|---------|-----------
`abcd`|`a`    |`ab`     |`dab`

还有一个内置函数`I.`(字母i 点)。表达式`(i. n)`会生成`n`个从零开始的连续整数。

`i. 4`   |`i. 6`       |`1 + i. 3`
---------|-------------|----------
`0 1 2 3`|`0 1 2 3 4 5`|`1 2 3`

如果`x`是一个列表，则表达式`(i. # x)`将会生成在列表`x`中所有可能的序号。

`x =: 'park'`|`# x`|`i. # x`
-------------|-----|--------
`park`       |`4`  |`0 1 2 3`
 
如果参数是一个列表`I.`将会生成一个数组：

```
    i. 2 3
0 1 2
3 4 5
```

`I.`函数还有一个双目的版本，叫作“取序号”。表达式`(x i. y)`将在`x`中寻找`y`的序号。

```
    'park' i. 'k'
3
```

找出的序号是`y`在`x`中第一次出现时的序号。

```
    'parka' i. 'a'
1
```

如果`x`中没有`y`，得到的序号将比最大序号多1。

```
    'park' i. 'j'
4
```

更多关于序号的变体，详见[第六章](chapter6.md)。

### 2.5.4 相等与匹配(Equality and Matching)

假设我们需要比较两个数组是否相同。有一个内置动词`-:`(减号冒号，叫作“匹配”)可以实现相关功能。它可以比较其两个参数是否有相同的外形以及相同的组成。

`X =: 'abc'`|`X -: X`|`Y =: 1 2 3 4`|`X -: Y`
------------|--------|--------------|--------
`abc`       |`1`     |`1 2 3 4`     |`0` 

无论参数是什么，匹配的结果将会是`0`或`1`。

注意：当列表是空的，字符列表与数字列表是匹配的。

```
    '' -: 0 $ 0
1
```

因为这两个数组有相同的外形，同时每一个元素都是相同的（根本就没有元素）。

另一个东西`=`(叫作“相等”) 会检测其参数是否相等，`=`会对其参数逐项比对，并得出一个和参数数组外形一致的逻辑数组。

`Y`      |`Y = Y`  |`Y = 2`
---------|---------|-------
`1 2 3 4`|`1 1 1 1`|`0 1 0 0`
 
因此，`=`的两个参数必须有相同的外形（或者至少有兼容的外形）。否则将会出错。

`Y`      |`Y = 1 5 6 4`|`Y=1 5 6`
---------|-------------|---------
`1 2 3 4`|`1 0 0 1`    |error
 
## 2.6 箱数组(Arrays of boxes)

### 2.6.1 连接

有一个内置函数是`;`(分号,称之为“连接”)。它将其两个参数连接在一起组成列表。两个参数可以是不同的种类。例如，我们可以将一个字符串和一个数字连接。

```
    A =: 'The answer is' ; 42

    A
+-------------+--+
|The answer is|42|
+-------------+--+
```

得到的结果`A`是一个长度为2的列表，并认为这是一个箱列表。在`A`的第一个箱中是字符串`'The answer is'`，在其第二个箱中是数字`42`。当箱在屏幕上显示时，其外侧会显示一个矩形框。

`A`|`0 { A`
---|-------
`+-------------+--+`<br> `\|The answer is\|42\|`<br>`+-------------+--+`|`+-------------+`<br>`\|The answer is\|`<br>`+-------------+`

无论一个箱内部是什么，其本身都是一个标量。因此箱可以像数字一样打包成数组。正因如此，`A`是一个标量列表。

`A`|`$ A`|`s =: 1 { A`|`# $ s`
---|-----|------------|-------
`+-------------+--+`<br>`\|The answer is\|42\|`<br>`+-------------+--+`|`2`|`+--+`<br>`\|42\|`<br>`+--+`|`0`

箱数组主要目的是把多个不同种类的值组合在一起。例如，一个用于记录购买详细信息(日期，数量，描述)当然的变量可以用箱列表来构建。

```
    P =: 18 12 1998 ; 1.99 ; 'baked beans'

    P
+----------+----+-----------+
|18 12 1998|1.99|baked beans|
+----------+----+-----------+
```

注意“连接”与“附加”的不同之处。“连接”可以结合不同种类的数据，而“附加”只能结合同种的数据。这意味着“附加”的两个参数要么都是数字数组，要么都是字符数组，要么都是箱数组，否则就会产生错误。

`'answer is'; 42`|`'answer is' , 42`
-----------------|------------------
`+---------+--+`<br>`\|answer is\|42\|`<br>`+---------+--+`|error

有时我们可能需要将数字和字符串结合起来，例如将计算结果与描述结合。我们可以像之前所述那样“连接”描述和数字。但是，有时可能需要更加平滑的展示方式，这就需要将数字转化为字符串，并附加在描述文字后面。

将数字转化为字符串可以用内置的“格式化”函数`":`(双引号 冒号)。在下面的例子中，`n`是一个数字，`s`是`n`格式化的值，是一个长度为2的字符串。

`n =: 42`|`s =: ": n`|`# s`|`'answer is ' , s`
---------|-----------|-----|------------------
`42`     |`42`       |`2`  |`answer is 42`

更多关于“格式化”的内容，详见[第十九章](chapter19.md)。接下来我们继续看箱。因为箱在显示时是用矩形包围的，所以它们用一个简单的表格形式来显示结果。

```
    p =: 4 1 $ 1 2 3 4
    q =: 4 1 $ 3 0 1 1

    2 3 $ ' p ' ; ' q ' ; ' p+q ' ; p ; q ; p+q
+---+---+-----+
| p | q | p+q |
+---+---+-----+
| 1 | 3 | 4   |
| 2 | 0 | 2   |
| 3 | 1 | 4   |
| 4 | 1 | 5   |
+---+---+-----+
```

### 2.6.2 封箱与开箱

存在内置函数`<`(左尖括号，叫作“装箱”)。对一个值应用`<`可以创建一个单个的箱。

```
    < 'baked beans'
+-----------+
|baked beans|
+-----------+
```

虽然一个箱可以包含数字，但其本身并非数字。要对一个箱中的值进行计算，就必须“打开”箱并取出值。函数`>`(右尖括号) 叫作“开箱”。

`b =: < 1 2 3`|`> b`
--------------|-----
`+-----+`<br>`\|1 2 3\|`<br>`+-----+`|`1 2 3`

将`<`想象成漏斗对理解很有帮助。宽的一端流入数据，窄的一端流出封了箱的标量——无维度量或点状数据。这种思想同样也适用于`>`。虽然箱是标量，可以使用逗号来组合成箱列表，但是使用分号往往更方便——毕竟同时有封箱和串接的功能。

`(< 1 1) , (< 2 2) , (< 3 3)`|`1 1 ; 2 2 ; 3 3`
-----------------------------|-----------------
`+---+---+---+`<br>`\|1 1\|2 2\|3 3\|`<br>`+---+---+---+`|`+---+---+---+`<br>`\|1 1\|2 2\|3 3\|`<br>`+---+---+---+`

## 2.7 总结

总而言之，每一个J语言中的数据对象，都是一个零维，一维或更高维度的数组。一个而数组可以是数字数组，字符数组，亦或是一个箱数组 (以及更多其他的可能情况)。
