# 第三章：定义函数
J语言已经自带了一些内置函数，就好比我们之前见到的`*`和`+`。在这一章我们开始了解如何通过不同的方式将这些内置函数组合，在一起来定义我们自己的函数。

## 3.1 重命名

定义函数最简单的方法是给内置函数起一个我们自己想要的名字。定义也是一种赋值。例如，定义`square`函数，让它和`*:`内置函数完全相同: 

```
    square =: *:

    square 1 2 3 4
1 4 9 16
```

当我们可以给出一个更好记的名字时，我们会很乐意去命名。我们也可以给一个内置函数起两个不同的名字，来分别表示单目和双目。

```
    Ceiling =: >.
Max =: >.
```

## 3.2 插入

回忆一下`(+/ 2 3 4)`表示`2+3+4`，同样的，`(*/ 2 3 4)`表示 2*3*4. 我么可以用赋值方式来定义一个叫`sum`的函数：

```
    sum =: + /

    sum 2 3 4
9
```

在此，`sum =: +/`展示出`+/`本身是一个表示函数的表达式。

表达式`+/`可以按如下方式理解: “插入”`(/)`作用于函数`+`来产生一个列表求和函数。

也就是说，`/`本身也是一种函数。他从左侧获取一个参数。其参数和结果都是函数。

## 3.3 术语：动词(Verbs)，算符(Operators)和副词(Adverbs) 

我们已经见到了其中的两种。第一种，那些像`+`和`*`之类的“普通”函数，这些函数被用于用数字来计算得到数字。在J语言中，这一类函数被称为“动词”。

第二种，像`/`之类的函数，这类函数用函数计算产生函数。这一类函数被叫做“算符”，用以和动词区分。

接受一个参数的算符被叫做“副词”。副词的参数一定在左侧。因此我们说，在表达时`(+ /)`中，副词`/`被应用于动词`+`来产生一个列表求和动词。

这些术语的名称源于英语语法：动词作用于事物，副词修饰动词。

## 3.4 交换

我们已经看到了一个副词`(/)`，接下来看另一个。副词`~`拥有交换左右参数的效果。

`'a' , 'b'`|`'a' ,~ 'b'`
-----------|------------
`ab`       |`ba`

对于一个有着参数`x`和`y`的双目函数`f`

```
x f~ y 表示 y f x 
```

另一个例子。回忆一下求余动词` |`，`2 | 7`在传统符号下意思是"7 mod 2"。我们可以定义一个`mod`动词: 

```
mod =: | ~
```

`7 mod 2`|`2 | 7`
---------|-------
`1`      |`1`

接下来画图说明。首先，是一个图，表示函数`f`应用了参数`y`得到结果`(f y)`。在这个图中，函数`f `被画成矩形，箭头表示参数输入及结果输出，每个箭头均标有一个表达式。

![](http://www.jsoftware.com/help/learning/diag01.gif)

接下来的一个图，表示双目函数`f`应用了参数`x`和`y`得到结果`(x f y)`。

![](http://www.jsoftware.com/help/learning/diag02.gif)

接下来是一个关于函数`(f~)`的图，表示为内部的一个函数`f`及交叉箭头的形式。

![](http://www.jsoftware.com/help/learning/diag03.gif)

## 3.5 结合(Bonding)

假设我们要定义一个动词`double`，`double x`表示`x * 2`。也就是说，`double`表示“乘以2”。我们用如下方式定义：

```
    double =: * & 2

    double 3
6
```

我们使用了双目函数`*,`并通过固定其两个参数中的一个为确定值（在这里是2），来得到一个单目函数。算符`&`可以将一个值以参数的形式结合到一个函数上。具体结构如下：如果`f`是一个双目函数，同时`k`将作为`f`右参数的值，那么：

```
(f & k) y 表示 y f k 
```

如果需要固定左参数，则：

```
(k & f) y 表示 k f y
```

例如，假设销售税的税率为10%，那么从购买价格计算税的函数是：

```
    tax =: 0.10 & *

    tax 50
5
```

这个图说明了函数`k&f`。

![](http://www.jsoftware.com/help/learning/diag04.gif)

## 3.6 术语：连词(Conjunctions)和名词(Nouns)

表达式`(*&2)`可以被描述为，算符`&`被应用了两个参数(动词`*`和数字`2`), 并得到了“翻倍”动词。

如同`&`一样的双目函数在J语言里被叫做“连词——因为它会将两个参数结合在一起。不同的是，副词只有一个参数。

J语言中每一个函数，无论是内置的还是用户定义的，总是术语如下四类之一：单目函数，双目函数，副词，连词。我们使用两用的符号。例如 - 表示两个动词：单目形式的取负，双目形式的减法。

J语言中的每一个表达式都是某种类型的值。所有不是函数的值都是数据(data)（事实上就是数组，正如我们之前所见到的那样）。

在J语言中，数据（也就是数组）被叫做“名词”（根据英语语法类比）。我们可以将某个值称为名词来强调它不是动词，或者强调它是一个可能有多个维度的数组。

## 3.7 函数的组合(Composition of Functions)

考虑如下英语表达式：the sum of the squares of the numbers`1 2 3`（1 2 3 的平方和），也就是`(1+4+9)`，即`14`。既然之前已经定义了动词`sum`和`square`，我们可以将J语言表达式写成如下形式：

```
    sum square 1 2 3
14
```

一个单独的平方和函数也可以写成`sum`和`square`的组合：

```
    sumsq =: sum @: square

    sumsq 1 2 3
14
```

符号`@:`(花A 冒号)被叫做“组合”算符。效果是，如果`f`和`g`都是动词，对于任意参数`y`

```
(f @: g) y 表示 f (g y)
```

下面是用于解释的图：

![](http://www.jsoftware.com/help/learning/diag05.gif)
 
可能你会问为什么写`(f @: g)`而不是直接写`(f g)`来表示组合。原因是`(f g)`表示其他内容，接下来会详细说明。
另一个组合的例子。华氏度可以通过函数`s`（减 32）和`m`（乘以`5%9`）的组合来计算的得到摄氏度。

```
s =: - & 32
m =: * & (5%9)
convert =: m @: s
```

`s 212`|`m s 212`|`convert 212`
-------|---------|-------------
`180`  |`100`    |`100`


为了清楚，这些表达式由显式命名的函数组合。当然也可以直接组合表达式：

```
    conv =: (* & (5%9)) @: (- & 32)

    conv 212
100
```

也可以直接应用表示函数的表达式而无需给出名字：

```
    (* & (5%9)) @: (- & 32) 212
100
```

之前的例子展示了单目函数与单目函数的组合。接下来的的例子展示单目函数与双目函数的组合。方式如下：

```
x (f @: g) y 表示 f (x g y)
```

例如，几个订单的总成本等于单位价格乘以数量，然后求和的得到结果。

```
P =: 2 3 NB. prices
Q =: 1 100 NB. quantities

total =: sum @: *
```

`P`  |`Q`    |`P*Q`  |`sum P * Q`|`P total Q`
-----|-------|-------|-----------|-----------
`2 3`|`1 100`|`2 300`|`302`      |`302`

更多关于组合的内容，详见第八章。

## 3.8 动词序列(Trains of Verbs)

考虑如下表达式"no pain, no gain（不劳无获）"。这是一种压缩惯用形式(compressed idiomatic form），即使结构上不符合语法也可以理解——这句话没有主动词。在J中有类似的概念：压缩惯用形式，它基于一个用于给简短函数列赋予意义的方案。 我们接下来来看相关内容。

### 3.8.1 钩子(Hooks)

回忆一下动词`tax`，我们之前定义用于计算10%税率下的税额。下面重复一下定义： 

```
tax =: 0.10 & *
```

购买时应支付的金额等于原价加上税额。下面的动词可以计算出应付金额： 

```
payable =: + tax
```

如果税前价格是`$50`：

`tax 50`|`50 + tax 50`|`payable 50`
--------|-------------|------------
`5`     |`55`         |`55`
 
在定义式`(payable =: + tax)`中，我们使用了两个动词`+`和`tax`的序列。这个序列放在赋值号的右侧，孤立存在。像这样孤立的动词序列被叫做一个“train”，一个两动词的的train被叫做一个“钩子（hook）”。

也可以通过用括号来孤立两个动词形成一个钩子： 

```
    (+ tax) 50
55
```

钩子的运作方式如下：如果`f`是一个双目函数，`g`是一个单目函数，那么对于任意参数`y`：

```
(f g) y 表示 y f (g y)
```

下面是图示：

![](http://www.jsoftware.com/help/learning/diag06.gif)
 
接下来是例子。回忆一下“向下取整”动词`<.`，可以用于获得其参数的整数部分。接下来我么可以通过判断某个数和它的整数部分是否相等，来测试这个数是不是整数。用来表示此意的动词就是一个钩子`(=<.)`： 

```
wholenumber =: = <.
```

`y =: 3 2.7`|`<. y`|`y = <. y`|`wholenumber y`
------------|------|----------|---------------
`3 2.7`     |`3 2` |`1 0`     |`1 0`

### 3.8.2 叉子(Forks)

一列数`L`的算数平均数就是`L`的和除以`L`的个数。（回忆一下动词`#`可以用来求数列的项数）

`L =: 3 5 7 9`|`sum L`|`# L`|`(sum L) % (# L)`
--------------|-------|-----|-----------------
`3 5 7 9`     |`24`   |`4`  |`6`

一个用于计算求和除以项数的动词可以定义为一个动词序列：`sum`，然后是`%`，然后是`#`。

```
    mean =: sum % #

    mean L
6
```

一个三动词的孤立序列被叫做叉子。运作方式如下：`f`是一个单目函数，`g`是一个双目函数，`h`是一个单目函数对于任意参数y， 

```
(f g h) y 表示 (f y) g (h y)
```

接下来是图示：

![](http://www.jsoftware.com/help/learning/diag08.gif)
  
另一个关于叉子的例子。求一个数列里数字的范围。可以通过叉子`smallest , largest`来实现。（中间的动词是逗号）

回忆一下在第一章提到过，用动词`>./`求最大值，用`<./`求最小值

```
range =: <./ , >./
```

`L`      |`range L`
---------|---------
`3 5 7 9`|`3 9`

钩子和叉子都是动词序列，也就是被称为“train”的动词。更多与train相关的内容，详见[第九章](chapter9.md)。

## 3.9 汇总 

接下来是更长的例子，混合使用了上述的特性。

一个想法是定义一个动词，用于显示所给定的数列，以及每个数占总和的百分比。

接下来先展示一下最终版本的程序，明确一下我们最终会得到的结果。在这里你不需要认真研究这个程序，因为接下来会做出详细解释。这个程序共有7行，定义了一个叫做`display`的动词来完成最终工作。 

```
frac =: % +/
percent =: (100 & *) @: frac
round =: <. @: (+ & 0.5)
comp =: round @: percent
br =: ,. ; (,. @: comp)
tr =: ('Data';'Percentages') & ,:
display =: tr @: br
```

首先从一些简单的数据开始：

```
data =: 3 1 4
```

我们看到动词`display`显示了每个数字占总数的百分比（舍入为整数形式）：`4`占`8`的`50%`。

```
    Display data
+----+-----------+
|Data|Percentages|
+----+-----------+
|3   |38         |
|1   |13         |
|4   |50         |
+----+-----------+
```

首先，我们的第一个目标是让每一个数字除以总和，得到每个数字的占比。钩子`(% +/)`很适合这个：它可以被叫做“除以总和”。我们将之命名为`frac`

```
 frac =: % +/
```

接下来可以看到 

`data` |`+/data`|`data % (+/data)`|`frac data`
-------|--------|-----------------|-----------
`3 1 4`|`8`     |`0.375 0.125 0.5`|`0.375 0.125 0.5`

可以把这些小数乘100来得到百分比。

```
percent =: (100 & *) @: frac
```

`data` |`frac data`      |`percent data`
-------|-----------------|--------------
`3 1 4`|`0.375 0.125 0.5`|`37.5 12.5 50`

接下来要将百分比转化为最接近的整数。可以通过对每个数字加`0.5`，再用动词`<.`向下取整得到。动词`round`定义如下：

```
round =: <. @: (+&0.5)
```

之后，从数据中得到要显示的值得动词定义如下：

```
comp =: round @: percent
```

`percent data`|`round percent data`|`comp data`
--------------|--------------------|-----------
`37.5 12.5 50`|`38 13 50`          |`38 13 50`

接下来我们希望在列的方向上显示数据和计算结果。可以通过内置动词`,.`(逗号 点，叫做“拆解项”)来吧数列转化为单列表格。

`data` |`,. data`        |`,. comp data`
-------|-----------------|--------------
`3 1 4`|`3`<br>`1`<br>`4`|`38`<br>`13`<br>`50`

为了制作显示在底部的那一行，我们定义了叉子`br`来将数据和计算结果连在一起： 

```
br =: ,. ; (,. @: comp)
```

`data` |`br data`
-------|---------
`3 1 4`|`+-+--+`<br>`\|3\|38\|`<br>`\|1\|13\|`<br>`\|4\|50\|`<br>`+-+--+`

为了添加顶部的一行（表头），将会用到内置动词`,:`(逗号 冒号，将在[第五章](chapter5.md)介绍)

```
tr =: ('Data';'Percentages') & ,:
```

`data` |`br data`|`tr br data`
-------|---------|------------
`3 1 4`|`+-+--+`<br>`\|3\|38\|`<br>`\|1\|13\|`<br>`\|4\|50\|`<br>`+-+--+`|`+----+-----------+`<br>`\|Data\|Percentages\|`<br>`+----+-----------+`<br>`\|3   \|38         \|`<br>`\|1   \|13         \|`<br>`\|4   \|50         \|`<br>`+----+-----------+`

然后将所有东西放到一起：

```
    display =: tr @: br

    display data
+----+-----------+
|Data|Percentages|
+----+-----------+
|3   |38         |
|1   |13         |
|4   |50         |
+----+-----------+
```

这个动词`display`包含了两个部分：用于计算百分比的函数`comp`，以及剩下用于表示结果的部分。通过改变`comp`的定义，我么可以`display`其它函数的值的表格。 例如我们将`comp`定义为内置的开方动词`(%:)`。

```
comp =: %:
```

我么也需要通过修改`tr`动词来改变表头：

```
    tr =: ('Numbers';'Square Roots') & ,:

    display 1 4 9 16
+-------+------------+
|Numbers|Square Roots|
+-------+------------+
| 1     |1           |
| 4     |2           |
| 9     |3           |
|16     |4           |
+-------+------------+
```

回顾一下，我们看到了一个小的J语言程序，包含了几种J语言中的特性：结合，组合，钩子和叉子。和许多J语言程序一样，这只是它众多可能的编写方式之一。

在这章中，我们首先介绍了定义函数的方式。有两种不同的函数：动词，算子。到目前为止，我们只是介绍了定义动词。下一章，我们将会介绍另一种定义动词的方式，在[第十三章](chapter13.md)将会介绍如何定义算子。 